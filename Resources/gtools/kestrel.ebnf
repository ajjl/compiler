# kestrelEBNF.gr the ebnf grammar for Kestrel
# Auhor: Douglas W. Jones
# Date:  Oct. 14, 2016 -- added enum keyword
# Taken from http://www.cs.uiowa.edu/~jones/compiler/kestrel/definition.shtml

> <kestrel program>
/ <empty>

<kestrel program> ::= <block> <end of file>

<block> ::= { <block element> [ ";" ] }

<block element> ::= <declaration>
                 |  <statement>

<declaration> ::= <identifier> ":" [ "private" | "restricted" ] <declarator>
<declarator> ::= <constant declarator>
              |  <type declarator>
              |  <exception declarator>
              |  <variable declarator>
              |  <procedure declarator>
              |  <function declarator>

<constant declarator> ::= "const" <expression>
                       |  "final" <expression>

<type declarator> ::= "type" <type>
                   |  "type" "-"

<exception declarator> ::= "exception"

<variable declarator> ::= "var" <type>

<procedure declarator> ::= "procedure"
                           [ <formal parameter list> ]
                           <body>
<function declarator> ::=  "function" <type>
                           [ <formal parameter list> ]
                           <body>

<body> ::= <block> "end"
        |  "-"
        |  "external"

<formal parameter list> ::= "(" <formal parameters> ")"
                         |  "[" <formal parameters> "]"
                         |  "{" <formal parameters> "}"
<formal parameters> ::= <parameter> { [ "," ] <parameter> }
<parameter> ::= <identifier> ":" <parameter declarer>
<parameter declarer> ::= <type>
                      |  "var" <type>
                      |  "final" <type>

<type> ::= <reference>
        |  <enumeration>
        |  <subrange>
        |  <pointer>
        |  <array>
        |  <set>
        |  <record>

<enumeration> ::= "enum" "(" <identifier> { [ "," ] <identifier>} ")"
               |  "enum" "[" <identifier> { [ "," ] <identifier>} "]"
               |  "enum" "{" <identifier> { [ "," ] <identifier>} "}"

<subrange> ::= <expression> ".." <expression>

<pointer> ::= "@" <type>

<array> ::= "array" <type> [ "of" ] <type>
         |  "array" "of" <type>

<set> ::= "set" [ "of" ] <type>

<record> ::= "record" [ "+" <reference> ] <block> "end"

<statement> ::= <do end>
             |  <if>
             |  <case>
             |  <loop>
             |  <exception handler>
             |  <raise>
             |  <procedure call>
             |  <assignment>

<do end> ::= "do"
                 <block>
             "end"

<if> ::= "if" <expression> [ "then" ]
             <block>
       [ "else"
             <block> ]
         "end"

<case> ::= "select" <expression> [ "in" ]
       { "case" <case label> { [ "," ] <case label> } ":"
             <block> }
       [ "else"
             <block> ]
         "end"

<case label> ::= <expression> [ ".." <expression> ]

<loop> ::= <while loop>
         |  <until loop>
         |  <for loop>

<while loop> ::= "while" <expression> [ "do" ]
                     <block>
                 "end"

<until loop> ::= "do"
                     <block>
                 "until" <expression>

<for loop> ::= "for" <identifier> "in" <type> [ "do" ]
                   <block>
               "end"

<exception handler> ::= "catch" <exception list> "in"
                            <block>
                      { "case" <exception list> ":"
                            <block> }
                      [ "else"
                            <block> ]
                        "end"
<exception list> ::= <reference> { [ "," ] <reference> }

<raise> ::= "raise" <reference>

<procedure call> ::= <reference>

<assignment> ::= <reference> "=" <expression>

<expression> ::= <comparand> [ <comparing operator> <comparand> ]
<comparand> ::= <term> { <adding operator> <term> }
<term> ::= <factor> { <multiplying operator> <factor> }

<comparing operator> ::= "="
                      |  "/="
                      |  ">"
                      |  ">="
                      |  "<"
                      |  "<="
                      |  "in"

<adding operator> ::= "+"
                   |  "-"
                   |  "|"

<multiplying operator> ::= "*"
                        |  "/"
                        |  "%"
                        |  "&"

<factor> ::= [ "-" | "~" ] <value>
<value> ::= <number>
         |  <string constant>
         |  "null"
         |  <reference>
         |  <subexpression>
<subexpression> ::= "(" <expression> ")"
                 |  "[" <expression> "]"
                 |  "{" <expression> "}"

<string constant> ::= <string>

<reference> ::= <identifier>
             |  <reference> "@"
             |  <reference> "." <identifier>
             |  <reference> <expression list>

<expression list> ::= "(" <expressions> ")"
                   |  "[" <expressions> "]"
                   |  "{" <expressions> "}"
<expressions> ::= <expression> { [ "," ] <expression> }
0Looking
